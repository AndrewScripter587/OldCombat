import ./oldcombat.mcbt
import ./z_configmenu.mcbt

function on_load minecraft:load{
    # you can use MC-Build without its internal scoreboard but this will limit the available features
    scoreboard objectives add mcb.internal dummy
    tellraw @a "Loading Old Combat Datapack by AndrewGaming587"
}

function on_tick minecraft:tick {
    execute as @a run {
        function block_offhand
        function disable_cooldown
        function sword_blocking
        function old_tool_damages
        function disable_sprinting
        function air_knockback
    }
    execute as @e run {
        function air_knockback
    }
    function disable_kb_resist
    function enforce_locked_settings
}

function enforce_locked_settings {
    execute if score DisableSprint OldCombatSettings matches 1 run scoreboard players set SprintingCrits OldCombatSettings 0
}

function config {
    tellraw @s "Old Combat Configuration"
    boolean_input <%"Sword Blocking"%> <%"If true, allows blocking using a sword. Sword blocking halves the damage taken for most damage types."%> OldCombatSettings SwordBlocking oldcombat:config
    boolean_input <%"Disable Offhand Slot"%> <%"If true, completely disables the offhand slot. Any items in the offhand slot when this is true will be dropped on the ground."%> OldCombatSettings DisableOffhand oldcombat:config
    boolean_input <%"Disable Attack Cooldown"%> <%"If true, completely disables the attack cooldown mechanic."%> OldCombatSettings DisableAttackCooldown oldcombat:config
    boolean_input <%"Old Tool Damages"%> <%"If true, tools (mainly axes) will do their pre-1.9 attack damage. Note: the attack damage tooltip on the tool will still show the normal amount, but the tool will deal the old amount of damage."%> OldCombatSettings OldToolDamage oldcombat:config
    boolean_input <%"Allow Critical Hits While Sprinting"%> <%"Enables getting critical hits while sprinting. After the combat update, you can no longer get a critical hit for your first hit during a sprint, you would just do a knockback attack instead. This setting, if true, allows players to crit for that first hit. This doesn't stop the knockback attack from happening, though, so you can get a crit and knockback attack at the same time. Due to technical restrictions, the critical hit will happen one tick after the player attacks a mob. Technical restrictions prevent the hitting a mob detection from happening on the same tick the attack happens. See https://modrinth.com/datapack/sprinting-crits for more about this mechanic."%> OldCombatSettings SprintingCrits disabled condition <%"if score DisableSprint OldCombatSettings matches 1"%> <%"This setting doesn't work when Disable Sprinting is true. Disable it to make this work again."%> oldcombat:config
    boolean_input <%"Disable Sprinting"%> <%"NOTICE: This doesn't prevent certain sprinting-related actions from happening, such as knockback attacks, the exhaustion from sprinting, the sprint-jump speed-boost, etc. This tweak only removes the attribute modifier (minecraft:sprinting) that increases your speed while sprinting. This will, however, prevent the sprinting-crits setting from working since it relies on this attribute modifier being applied to detect sprinting"%> OldCombatSettings DisableSprint oldcombat:config
    boolean_input <%"Disable Knockback Resistance"%> <%"Removes all forms of knockback resistance such as the partial knockback resistance provided by netherite armor, the full knockback resistance iron golems and wardens have, etc."%> OldCombatSettings DisableKBResist oldcombat:config
    boolean_input <%"Knockback Effective In Air"%> <%"NOTICE: This will NOT apply to players, UNLESS you have a mod installed that allows editing of player NBT data. Makes knockback from attacks as effective in the air as it is on the ground."%> OldCombatSettings AirKB oldcombat:config
    numeric_input <%"Air Knockback Strength"%> <%"Amount of vertical knockback applied to entities in the air by the above setting, in centimeters per tick. For reference, 1 cubic meter = 1 minecraft block, 100 cm = 1 meter, 20 ticks in 1 second."%> OldCombatSettings AirKBForce 10 disabled condition <%"if score AirKB OldCombatSettings matches 0"%> oldcombat:config

    REPEAT (10,19) as i {
        tellraw @s ""
    }
}

function defaults minecraft:load {
    scoreboard objectives add OldCombatSettings dummy
    tellraw @a "Old Combat: Loading Default Settings"
    scoreboard players set DisableOffhand OldCombatSettings 1
    scoreboard players set DisableAttackCooldown OldCombatSettings 1
    scoreboard players set SwordBlocking OldCombatSettings 1
    scoreboard players set OldToolDamage OldCombatSettings 1
    scoreboard players set SprintingCrits OldCombatSettings 1
    scoreboard players set DisableSprint OldCombatSettings 0
    scoreboard players set DisableKBResist OldCombatSettings 1
    scoreboard players set AirKB OldCombatSettings 1
    scoreboard players set AirKBForce OldCombatSettings 40
}

function disable {
    scoreboard objectives add OldCombatSettings dummy
    tellraw @a "Old Combat: Disabling all settings"
    scoreboard players set DisableOffhand OldCombatSettings 0
    scoreboard players set DisableAttackCooldown OldCombatSettings 0
    scoreboard players set SwordBlocking OldCombatSettings 0
    scoreboard players set OldToolDamage OldCombatSettings 0
    scoreboard players set SprintingCrits OldCombatSettings 0
    scoreboard players set DisableSprint OldCombatSettings 0
    scoreboard players set DisableKBResist OldCombatSettings 0
    scoreboard players set AirKB OldCombatSettings 0
    scoreboard players set AirKBForce OldCombatSettings 40
}

function air_knockback {
    execute if score AirKB OldCombatSettings matches 1 if data entity @s {HurtTime:10s} run {
        execute unless data entity @s {Motion:[0.0d,0.0d,0.0d]} unless data entity @s {OnGround:true} store result entity @s Motion[1] double 0.01 run scoreboard players get AirKBForce OldCombatSettings
    }
}

function disable_cooldown {
    execute if score DisableAttackCooldown OldCombatSettings matches 1 run {
        attribute @s attack_speed modifier remove minecraft:base_attack_speed
        attribute @s attack_speed modifier add minecraft:base_attack_speed 100 add_value
    }
}

function disable_sprinting {
    execute if score DisableSprint OldCombatSettings matches 1 run {
        attribute @s movement_speed modifier remove minecraft:sprinting
    }
}


function disable_kb_resist {
    execute if score DisableKBResist OldCombatSettings matches 1 run {
        execute as @e run attribute @s knockback_resistance modifier add oldcombat:nokbresist -1 add_multiplied_total
    } else execute run {
        execute as @e run attribute @s knockback_resistance modifier remove oldcombat:nokbresist
    }
}

function old_tool_damages {
    execute if score OldToolDamage OldCombatSettings matches 1 if items entity @s weapon.mainhand #axes run {
        attribute @s attack_damage modifier remove base_attack_damage
        execute if items entity @s weapon.mainhand wooden_axe run attribute @s attack_damage modifier add base_attack_damage 2 add_value
        execute if items entity @s weapon.mainhand stone_axe run attribute @s attack_damage modifier add base_attack_damage 3 add_value
        execute if items entity @s weapon.mainhand iron_axe run attribute @s attack_damage modifier add base_attack_damage 4 add_value
        execute if items entity @s weapon.mainhand golden_axe run attribute @s attack_damage modifier add base_attack_damage 2 add_value
        execute if items entity @s weapon.mainhand diamond_axe run attribute @s attack_damage modifier add base_attack_damage 5 add_value
        execute if items entity @s weapon.mainhand netherite_axe run attribute @s attack_damage modifier add base_attack_damage 6 add_value
    } else execute run {
        attribute @s attack_damage modifier remove oldcombat:olddamage
    }
}

tag damage_type bypasses_sword_blocking {
    #minecraft:bypasses_shield
    #minecraft:is_fall
    #minecraft:bypasses_invulnerability
    minecraft:starve
    #minecraft:is_drowning
    minecraft:in_fire
    minecraft:on_fire
    minecraft:lava
    minecraft:campfire
    minecraft:hot_floor
    minecraft:freeze
}

function sword_blocking {
    execute if score SwordBlocking OldCombatSettings matches 1 if items entity @s weapon.mainhand #minecraft:swords[!blocks_attacks] run {
        modify_held_components {"minecraft:blocks_attacks":{disable_cooldown_scale:0,damage_reductions:[{base:0,factor:0.5,horizontal_blocking_angle:180}],block_sound:"entity.player.hurt",item_damage:{threshold:0f,base:0f,factor:0f},bypassed_by:"#oldcombat:bypasses_sword_blocking"}}
    } else execute if score SwordBlocking OldCombatSettings matches 0 run {
        remove_held_component "minecraft:blocks_attacks"
    }
}

function block_offhand {
    execute if score DisableOffhand OldCombatSettings matches 1 if items entity @s weapon.offhand * run {
        execute at @s anchored eyes positioned ^ ^ ^ positioned ~ ~-0.1 ~ run function throw_offhand
        tellraw @s {"color":"red","text":"[Old Combat]: Offhand is disabled. Your offhanded item(s) were dropped on the ground."}
        item replace entity @s weapon.offhand with air
    }
}

function throw_offhand {
    throw_offhand_stack 0.4 80 250
}

function throw_held {
    throw_held_stack 0.4 80 250
}

